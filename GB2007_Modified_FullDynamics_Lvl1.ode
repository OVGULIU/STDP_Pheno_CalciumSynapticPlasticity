# auto parameters
@ NPR=400, NMAX=40000, DSMAX=0.01, DS=.01, PARMIN=0, PARMAX=2
@ AUTOXMIN=0, AUTOXMAX=2, AUTOYMIN=0, AUTOYMAX=210, AUTOVAR=Ta

###### VARIABLES ######
init Ca=2
init B1=0.0,B2=0.0,B3=0.0,B4=0.0
init B5=0.0,B6=0.0,B7=0.0
init B8=0.0,B9=0.0,B10=0.0
init B11=0.0,B12=0.0,B13=0.0
init PP1=0.0, I1P=0.0
init TA=132.3964
# AMPA populations
init n0=100, n1=0, n2=0


####### PARAMS #######

# CALCIUM PARAMETERS
param tauCa=12, CaBas=0.2

# CaMKII PATHWAY PARAMETERS
param b0i=33.3
param K5=0.1, CaM=0.1
param L1=0.1, L2=0.025, L3=0.32, L4=0.40
param k6=6, k7=6
param PP10=0.2
param k12=6000
param KM=0.4
param k11=500, km11=0.1
param I10=1

param Kdcan=0.053, ncan=3
param kcan0_I1=0.1, kcan_I1=18
param kcan0_endo=0.1, kcan_endo=18
param kcan0_pase=0.1, kcan_pase=18

param Kdpka=0.11, npka=8
param kpka0_I1=0.00359, kpka_I1=100
param kpka0_phos=0.00359, kpka_phos=100

param kCK2_exo=0.005

# AMPA TRAFFICKING PARAMETERS
# conductance of one channel is assumed to be 20pS, and we assume a total of 1000 AMPAr
param N=1000
param g0=0.0010, g1=0.0017, g2=0.0024


####### CONSTRAINTS & DEFINITIONS #######

# CaMKII CONSERVATION
rr=sum(0,12)of(shift(B1,i'))
# p0 is whats left from total
B0=b0i-rr

phossum=B1 + 2*(B2 + B3 + B4) + 3*(B5 + B6 + B7 + B8) + 4*(B9 + B10 + B11) + 5*B12 + 6*B13


###### KINETICS ######

k10=k12*PP1/(KM + phossum)
C=CaM/(1 + L4/Ca + L3*L4/(Ca^2) + L2*L3*L4/(Ca^3) + L1*L2*L3*L4/(Ca^4))
gamma=C/(K5+C)

# PKA activity
vPKA_I1=kpka0_I1 + kpka_I1/(1 + (Kdpka/C)^npka)
vPKA_phos=kpka0_phos + kpka_phos/(1 + (Kdpka/C)^npka)

# CaN activity
vCaN_I1=kcan0_I1 + kcan_I1/(1 + (Kdcan/C)^ncan)
vCaN_endo=kcan0_endo + kcan_endo/(1 + (Kdcan/C)^ncan)
vCan_pase=kcan0_pase + kcan_pase/(1 + (Kdcan/C)^ncan)

# CaMKII activity
vCK2_exo=kCK2_exo*phossum


###### DYNAMICS ######

# Ca2+ equation
Ca'=(CaBas-Ca)/tauCa

# CaMKII concentration equations
B1' = 6*k6*gamma^2*B0 - 4*k6*gamma^2*B1 - k7*gamma*B1 - k10*B1 + 2*k10*(B2 + B3 + B4)
#
B2' = k7*gamma*B1 + k6*gamma^2*B1 - 3*k6*gamma^2*B2 - k7*gamma*B2 - 2*k10*B2 + k10*(2*B5 + B6 + B7)
B3' = 2*k6*gamma^2*B1 - 2*k7*gamma*B3 - 2*k6*gamma^2*B3 - 2*k10*B3 + k10*(B5 + B6 + B7 + 3*B8) 
B4' = k6*gamma^2*B1 - 2*k7*gamma*B4 - 2*k6*gamma^2*B4 - 2*k10*B4 + k10*(B6 + B7)
#
B5' = k7*gamma*B2 + k7*gamma*B3 + k6*gamma^2*B2 - k7*gamma*B5 - 2*k6*gamma^2*B5 - 3*k10*B5 + k10*(2*B9 + B10)
B6' = k6*gamma^2*B2 + k6*gamma^2*B3  + 2*k7*gamma*B4 - k6*gamma^2*B6 - 2*k7*gamma*B6 - 3*k10*B6 + k10*(B9 + B10 + 2*B11)
B7' = k6*gamma^2*B2 + k7*gamma*B3 + 2*k6*gamma^2*B4 - k6*gamma^2*B7 - 2*k7*gamma*B7 - 3*k10*B7 + k10*(B9 + B10 + 2*B11)
B8' = k6*gamma^2*B3 - 3*k7*gamma*B8 - 3*k10*B8 + k10*B10
#
B9' = k7*gamma*B5 + k6*gamma^2*B5 + k7*gamma*B6 + k7*gamma*B7 - k6*gamma^2*B9 - k7*gamma*B9 - 4*k10*B9 + 2*k10*B12
B10'= k6*gamma^2*B5 + k6*gamma^2*B6 + k7*gamma*B7 + 3*k7*gamma*B8 - 2*k7*gamma*B10 - 4*k10*B10 + 2*k10*B12
B11'= k7*gamma*B6 +  k6*gamma^2*B7 - 2*k7*gamma*B11 - 4*k10*B11 + k10*B12
#
B12'= k7*gamma*B9 + k6*gamma^2*B9 + 2*k7*gamma*B10 + 2*k7*gamma*B11 - k7*gamma*B12 - 5*k10*B12 + 6*k10*B13
#
B13'= k7*gamma*B12 - 6*k10*B13

# Phosphatase dynamics
PP1'= -k11*I1P*PP1 + km11*(PP10 - PP1)
# ORIGINAL: I1P'= -k11*I1P*PP1 + km11*(PP10 - PP1) + vPKA*I10 - vCaN*I1P
I1P'= -k11*I1P*PP1 + km11*(PP10 - PP1) + vPKA*(I10-I1P) - vCaN*I1P

# Membrane AMPA dynamics
n0'= vCaMKII_exo*(N-n0) - vCaN_endo*n0 - vPKA_phos*((phossum/b0i)-1)*(2*n0) + vCaN_pase*n1
n1'= vPKA_phos*((phossum/b0i)-1)*(2*n0-n1) - vCaN_pase*(n1-2*n2)
n2'= vPKA_phos*((phossum/b0i)-1)*(n1) - vCaN_pase*(2*n2)

W= g0*n0 + g1*n1 + g2*n2

###### AUXILIARIES ######

ta'=-ta+phossum
aux act=phossum
aux auxB0=B0
aux auxC=C
aux auxW=W


###### CONTROLS ######

@ total=300,dt=0.002
@ bound=100000
@ maxstor=100000 
@ njmp=10

done	
