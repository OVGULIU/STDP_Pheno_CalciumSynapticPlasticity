# Naming conventions
# k --- reaction constant
# r --- reaction rate ------------------------------- r_{11} = k_{11}*[C_p]*[PP1_{free}]
# K --- dissociation constant ----------------------- K_5 = k_{-5} / k_{5}
# KM -- Michaelis constant for catalytic reaction --- KM_Dpu = (k_{-11} + k_{12})/k_{11}

# auto parameters
@ NPR=400, NMAX=40000, DSMAX=0.01, DS=.01, PARMIN=0, PARMAX=2
@ AUTOXMIN=0, AUTOXMAX=2, AUTOYMIN=0, AUTOYMAX=210, AUTOVAR=Ta

###### VARIABLES ######
init Ca=1

init gamma=0.05

init B1=0.0
init B2=0.0,B3=0.0,B4=0.0
init B5=0.0,B6=0.0,B7=0.0, B8=0.0
init B9=0.0,B10=0.0, B11=0.0
init B12=0.0
init B13=0.0

init PP1=0.0, I1P=0.0

init TA=132.3964

# CaMKII-NR2B variables
init mu=0
init U=0

# AMPA populations
# init n0=100, n1=0

####### PARAMS #######

## CALCIUM PARAMETERS
param tauCa=0.012, CaBas=0.2

## CaMKII PATHWAY PARAMETERS
# total CaMKII conc
param b0i=33.3
# total CaM conc
param CaM=10
# Ca2+/CaM binding dissociation const, from G&B 2007. Assume K9=K5
param K5=0.1, K9=0.0001

# Intermediate Ca to CaM association rates, from G&B 2007
param L1=0.1, L2=0.025, L3=0.32, L4=0.40
# Autophosphorilation by unphosphorilated subunit - reaction constant from G&B 2007
param k6=6

# Autophosphorylation by phosphorylated subunit: CaM-bound, free, T305-phosphorylated - from G&B 2007
# Let's settle on these values
param k7=6, k8=6, k19=6

# T-305 autophosphorylation kinetics
param k17=10, k18=0.0005

# PP1-CaMKII to dephosCaMKII constants + Michaelis: Dpu -> Duu (G&B 2007), Dpp -> Dpu, Dup -> Du
param KM=0.4
param k12=6000

param tauNRG=0.01


# I1-PP1 kinetics
param k11=500, km11=0.1
param I10=1
param PP10=0.2

param Kdcan=0.053, ncan=3
param kcan0_I1=0.1, kcan_I1=18
# Values for the next two lines need to be assessed
param kcan0_endo=0.1, kcan_endo=18
param kPP10_pase=0.1, kPP1_pase=18

param Kdpka=0.11, npka=8
param kpka0_I1=0.00359, kpka_I1=100
# Values for the next line need to be assessed
param kpka0_phos=0.00359, kpka_phos=100

# Values for the next two lines need to be assessed
param kCK2_exo=0.0005
param kNMDA_bind=0.1

# AMPA TRAFFICKING PARAMETERS
# conductance of one channel is assumed to be 20pS, and we assume a total of 1000 AMPAr
param N=1000
param g0=0.0010, g1=0.0017, g2=0.0024

# NMDA PARAMETERS
param M=100

####### CONSTRAINTS & DEFINITIONS #######

# CaMKII CONSERVATION
rr=sum(0,12)of(shift(B1,i'))
# p0 is whats left from total
B0=b0i-rr

Sp=B1 + 2*(B2 + B3 + B4) + 3*(B5 + B6 + B7 + B8) + 4*(B9 + B10 + B11) + 5*B12 + 6*B13
Su=6*b0i-Sp
AMPA_bnd=2*(B2+B3+B4) + 6*(B5+B6+B7+B8) + 12*(B9+B10+B11) + 20*B12 + 30*B13

###### KINETICS ######

C=CaM/(1 + L4/Ca + L3*L4/(Ca^2) + L2*L3*L4/(Ca^3) + L1*L2*L3*L4/(Ca^4))

#zeta=1-gamma - K5*gamma/(C-(gamma*Su+gamma_star*Sp))
#zeta_star=1-gamma_star - K9*gamma_star/(C-(gamma*Su+gamma_star*Sp))
zeta=0
zeta_star=0

quad_a(x)=k12*k17*K9*Sp*(1-x)
quad_b(x)=-C*k12*k17*K9 + C*k12*k17*K9*x + k17*K5*K9*x - k12*k18*K5*Sp*x + k12*k17*K9*Su*x - k12*k17*K9*Su*x^2
quad_c(x)=k12*k18*K5*x*(C-x*Sp)
quad(x)=(-quad_b(x)-sqrt(quad_b(x)^2-4*quad_a(x)*quad_c(x)))/(2*quad_a(x))
#gamma_star=quad(gamma)
gamma_star=0

energy(x)=k18*K5*x*((KM+Su+Su*(1-x)+Sp*(1-quad(x)))*(C-x*Su-quad(x)*Sp)-K5*Su*x-K9*Sp*quad(x))+k12*PP1*(C-x*Su-quad(x)*Sp)*(K5*x-(1-x)*(C-x*Su-quad(x)*Sp))
gamma'=(1/tauNRG)*energy(gamma)

k10=k12*PP1/(KM + (1+zeta_star)*Sp + zeta*(Su))

# PKA activity
vPKA_I1=kpka0_I1 + kpka_I1/(1 + (Kdpka/C)^npka)
vPKA_phos=kpka0_phos + kpka_phos/(1 + (Kdpka/C)^npka)

# CaN activity
vCaN_I1=kcan0_I1 + kcan_I1/(1 + (Kdcan/C)^ncan)
vCaN_endo=kcan0_endo + kcan_endo/(1 + (Kdcan/C)^ncan)

# PP1 activity
vPP1_pase=kPP10_pase + kPP1_pase/(1 + (Kdcan/C)^ncan)

# CaMKII activity
vCK2_exo=kCK2_exo*Sp

###### DYNAMICS ######

# Ca2+ equation
# Ca'=(CaBas-Ca)/tauCa
Ca'=0

# Useful qties for following equations
chi=k7*gamma_star + k8*(1-gamma_star-zeta_star) + k19*zeta_star
nu=k10*(1+zeta_star)

# CaMKII concentration equations
B1' = 6*k6*gamma^2*B0 - 4*k6*gamma^2*B1 - chi*gamma*B1 + nu*(2*(B2+B3+B4)-B1) - kNMDA_bind*(M-mu)*B1
#
B2' = k6*gamma^2*B1 + chi*gamma*B1 + nu*(3*(B5+B6+B7+B8)-2*B2) - 3*k6*gamma^2*B2 - chi*gamma*B2 - 2*kNMDA_bind*(M-mu)*B2
B3' = 2*k6*gamma^2*B1 + nu*(3*(B5+B6+B7+B8)-2*B3) - 3*k6*gamma^2*B3 - chi*gamma*B3 - 2*kNMDA_bind*(M-mu)*B3
B4' = k6*gamma^2*B1 + nu*(3*(B5+B6+B7+B8)-2*B4) - 2*k6*gamma^2*B4 - 2*chi*gamma*B4 - 2*kNMDA_bind*(M-mu)*B4
#
B5' = k6*gamma^2*(B2+B3) + chi*gamma*B2 + nu*(4*(B9+B10+B11)-3*B5) - 2*k6*gamma^2*B5 - chi*gamma*B5 - 3*kNMDA_bind*(M-mu)*B5
B6' = k6*gamma^2*(B2+B3) + 2*chi*gamma*B4 + nu*(4*(B9+B10+B11)-3*B6) - k6*gamma^2*B6 - 2*chi*gamma*B6 - 3*kNMDA_bind*(M-mu)*B6
B7' = k6*gamma^2*(B2+2*B4) + chi*gamma*B3 + nu*(4*(B9+B10+B11)-3*B7) - k6*gamma^2*B7 - 2*chi*gamma*B7 - 3*kNMDA_bind*(M-mu)*B7
B8' = k6*gamma^2*B3 + nu*(4*(B9+B10+B11)-3*B8) - 3*chi*gamma*B8 - 3*kNMDA_bind*(M-mu)*B8
#
B9' = k6*gamma^2*B5 + chi*gamma*(B6+B7) + nu*(5*B12-4*B9) - k6*gamma^2*B9 - chi*gamma*B9 - 4*kNMDA_bind*(M-mu)*B9
B10'= k6*gamma^2*(B5+B6) + chi*gamma*(B7+B8) + nu*(5*B12-4*B10) - 2*chi*gamma*B10 - 4*kNMDA_bind*(M-mu)*B10
B11'= k6*gamma^2*B7 + chi*gamma*B6 + nu*(5*B12-4*B11) - 2*chi*gamma*B11 - 4*kNMDA_bind*(M-mu)*B11
#
B12'= k6*gamma^2*B9 + chi*gamma*(B9+2*B10+2*B11) + nu*(6*B13-5*B12) - chi*gamma*B12 - 5*kNMDA_bind*(M-mu)*B11
#
B13'= chi*gamma*B12 - nu*6*B13 - 6*kNMDA_bind*(M-mu)*B13

# Phosphatase dynamics
PP1'= -k11*I1P*PP1 + km11*(PP10 - PP1)
# ORIGINAL: I1P'= -k11*I1P*PP1 + km11*(PP10 - PP1) + vPKA*I10 - vCaN*I1P
I1P'= -k11*I1P*PP1 + km11*(PP10 - PP1) + vPKA_I1*(I10-I1P) - vCaN_I1*I1P

# NMDA binding dynamics - Version 1
mu'=kNMDA_bind*(M-mu)*Sp
U'=kNMDA_bind*(M-mu)*AMPA_bnd

# Membrane AMPA dynamics
#n0'=vCK2_exo*(N-n0-n1) - vCaN_endo*n0 - vPKA_phos*n0*U + vPP1_pase*n1*PP1
#n1'=vPKA_phos*n0*U - vPP1_pase*n1*PP1

#W=g0*n0 + g1*n1

###### AUXILIARIES ######

ta'=-ta+Sp
aux act=Sp
aux auxB0=B0
aux auxC=C
#aux auxW=W
aux auxgam=gamma
aux auxgamst=gamma_star
aux auxzet=zeta
aux auxzetst=zeta_star
aux nrg=energy(gamma)
aux freeC=C-gamma*Su-gamma_star*Sp
###### CONTROLS ######

@ total=300,dt=0.002
@ bound=100000
@ maxstor=100000 
@ njmp=10
@ seed=30

done	
